#!/usr/bin/env perl
# -*- cperl -*-

use 5.016;
use strict;
use warnings FATAL => qw(io regexp severe syntax uninitialized void);

##
use FindBin qw();

my $_libexec;


BEGIN {
  $_libexec = $FindBin::RealBin;
}

use lib $_libexec;

##
use Cetmodules qw();
use Cetmodules::Util qw(error_exit info notify to_ups_version verbose);
use Cwd qw(:DEFAULT chdir abs_path);
use File::Basename;
use File::Spec; # For catfile;
use IO::File qw();
use IO::Handle qw();

##
use warnings FATAL => qw(Cetmodules);

if( $#ARGV < 3 ) {
    print "\n";
    print "ERROR: unspecified parameters - only have $#ARGV \n";
    print "USAGE: edit_product_deps <file> <product> <version> <dryrun:yes|no>\n";
    exit 1;
}

my $pdfile = $ARGV[0];
my $product = $ARGV[1];

error_exit("ill-formed new version: $ARGV[2]")
  unless $ARGV[2] =~ m&^(?<quote>["']?)(?<ver>.*)\g{quote}$&;
my $new_version = $+{ver};
$new_version =~ m&\Av[0-9]&msx or
  $new_version = to_ups_version($+{ver});
my $dryrun = ($ARGV[3] // '') =~ m&^(y(?:es)?|1|t(?:rue))&;

my ($pdfilenew, $fh_out);

if ($dryrun) {
  notify("[DRY RUN]: these changes would have been made:\n\n");
} else {
  $pdfilenew = "$pdfile.new";
  info("writing $pdfile as $pdfilenew\n\n");
  $fh_out = IO::File->new("$pdfilenew", ">") or
    error_exit("failed to open $pdfilenew for write");
}

my $changed;
my $get_phash="";
my $get_quals="";
my $get_fragment="";
my $fh_in = IO::File->new("$pdfile", "<") or
  error_exit("failed to open $pdfile for read");

while (my $line = <$fh_in>) {
  chomp $line;
  my $newline = $line;
  if ($newline =~ s&^(\s*parent\s+\Q$product\E\s+)[^#\s]+&$1$new_version& or
      ($get_phash eq "true" and
       $newline =~ s&^(\s*\Q$product\E\s+)[^-\s]\S+&$1$new_version&)) {
  } elsif ($line =~ m&^\s*parent\s+\Q$product\E&) {
    info(sprintf("version information for $product is set by project() in %s/CMakeLists.txt\n",
                 dirname(dirname($pdfile))));
  } elsif ($line =~ m&^\s*product\b&) {
    $get_phash="true";
  } elsif ($line =~ m&^\s*end_product_list\b&) {
    $get_phash="";
  }
  if ($newline ne $line) {
    ++$changed;
    if ($dryrun) {
      print <<EOF;
  ----------------------------------
  old line: $line
  ==================================
  new line: $newline
  ++++++++++++++++++++++++++++++++++
EOF
    } else {
      $line = $newline;
    }
  }
  $fh_out->print("$line\n") if $fh_out;
}
$fh_out->close() if $fh_out;
$fh_in->close();

if ($changed) {
  if ($dryrun) {
    notify(sprintf("[DRY_RUN] would have made $changed edit%s to $pdfile\n",
                   ($changed != 1) ? 's' : ''));
  } else {
    rename($pdfile, "$pdfile.bak") or error_exit("failed to save backup $pdfile.bak ($!)");
    rename($pdfilenew, $pdfile) or error_exit("failed to install $pdfilenew as $pdfile ($!)");
    info(sprintf("made $changed edit%s to $pdfile\n",
                 ($changed != 1) ? 's' : ''));
    my $colordiff = (system("type -t colordiff >/dev/null 2>&1") == 0) ? ' | colordiff' : '';
    verbose(`diff -u "$pdfile.bak" "$pdfile"$colordiff`);
  }
} else {
  info(sprintf("%sno changes necessary to $pdfile%s",
               ($dryrun) ? "[DRY_RUN] " : '',
               ($pdfilenew and -e $pdfilenew) ? ": removing $pdfilenew" : ''));
  unlink $pdfilenew if ($pdfilenew and -e $pdfilenew);
}

1;
